#!/usr/bin/perl

use strict;
use warnings;

use File::Basename qw( basename );
use Getopt::Long::Descriptive;
use JSON;
use LWP::UserAgent;

my $basename = basename $0;
my( $opt, $usage ) = describe_options( <<"END" . 'OPTIONS',
USAGE
    $basename [<args>] URL

DESCRIPTION
    $basename fetches paginated OPTIMADE query results.

END
    [ 'page-limit=i', 'limit the number of pages' ],
    [ 'format=s', 'output format (default: json)' ],
    [],
    [ 'help', 'print usage message and exit', { shortcircuit => 1 } ],
);

if( $opt->help ) {
    print $usage->text;
    exit;
}

my $page_limit = $opt->page_limit;

my $format = 'json';
if( $opt->format && $opt->format =~ /^json(l(ines)?)?$/ ) {
    $format = 'jsonlines' if $opt->format =~ /^jsonl(ines)?$/;
} else {
    die "unknown output format '". $opt->format . "'\n";
}

my $URL = shift @ARGV;

my $ua = LWP::UserAgent->new( agent => 'optimade-client/0.0.0 (contact cod-bugs@ibt.lt)' );

my @data;
while( $URL ) {
    sleep 1;
    print STDERR "GET $URL\n";
    my $response = $ua->get($URL);
    die $response->status_line unless $response->is_success;
    my $json = decode_json( $response->decoded_content );
    if( $format eq 'json' ) {
        push @data, @{$json->{data}};
    } else {
        for my $entry (@{$json->{data}}) {
            my $output = encode_json( $entry );
            $output =~ s/\n//g;
            print $output, "\n";
        }
    }
    if( $json->{links}{next} ) {
        $URL = $json->{links}{next};
        $URL = $URL->{href} if ref $URL eq 'HASH';
    } else {
        last;
    }
    if( defined $page_limit ) {
        $page_limit--;
        last unless $page_limit;
    }
}

print encode_json( \@data ) if $format eq 'json';
